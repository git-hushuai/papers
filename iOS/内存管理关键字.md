#### weak表结构

> weak_table_t是一个全局weak引用的表，使用对象的地址作为key，用weak_entry_t类型结构体对象作为value。


```
objc-weak.h 文件关于weak_table_t结构体定义
/**
 * The global weak references table. Stores object ids as keys,
 * and weak_entry_t structs as their values.
 */
struct weak_table_t {
    weak_entry_t *weak_entries;// 保留了所有指向对象的weak指针
    size_t    num_entries;// weak对象的存储空间
    uintptr_t mask;// 
    uintptr_t max_hash_displacement;
};
```
> weak_entry_t 是存储在弱引用表中的一个内部结构体，它负责维护和存储指向一个对象的所有弱引用hash表，其定义如下：

```
struct weak_entry_t {
    DisguisedPtr<objc_object> referent;
    union {
        struct {
            weak_referrer_t *referrers;
            uintptr_t        out_of_line_ness : 2;
            uintptr_t        num_refs : PTR_MINUS_2;
            uintptr_t        mask;
            uintptr_t        max_hash_displacement;
        };
        struct {
            // out_of_line_ness field is low bits of inline_referrers[1]
            weak_referrer_t  inline_referrers[WEAK_INLINE_COUNT];
        };
    };

    bool out_of_line() {
        return (out_of_line_ness == REFERRERS_OUT_OF_LINE);
    }

    weak_entry_t& operator=(const weak_entry_t& other) {
        memcpy(this, &other, sizeof(other));
        return *this;
    }

    weak_entry_t(objc_object *newReferent, objc_object **newReferrer)
        : referent(newReferent)
    {
        inline_referrers[0] = newReferrer;
        for (int i = 1; i < WEAK_INLINE_COUNT; i++) {
            inline_referrers[i] = nil;
        }
    }
};
```

> 在weak_entry_t 结构体中，DisguisedPtr referent 是对泛型对象的指针做了一层封装，通过这个泛型类来解决内存泄露的问题，
> objc_object 是weak_entry_t表中weak弱引用对象的范型对象的结构体。

```
struct objc_object {
private:
    isa_t isa;

public:

    // ISA() assumes this is NOT a tagged pointer object
    Class ISA();

    // getIsa() allows this to be a tagged pointer object
    Class getIsa();

    // initIsa() should be used to init the isa of new objects only.
    // If this object already has an isa, use changeIsa() for correctness.
    // initInstanceIsa(): objects with no custom RR/AWZ
    // initClassIsa(): class objects
    // initProtocolIsa(): protocol objects
    // initIsa(): other objects
    void initIsa(Class cls /*nonpointer=false*/);
    void initClassIsa(Class cls /*nonpointer=maybe*/);
    void initProtocolIsa(Class cls /*nonpointer=maybe*/);
    void initInstanceIsa(Class cls, bool hasCxxDtor);

    // changeIsa() should be used to change the isa of existing objects.
    // If this is a new object, use initIsa() for performance.
    Class changeIsa(Class newCls);

    bool hasNonpointerIsa();
    bool isTaggedPointer();
    bool isBasicTaggedPointer();
    bool isExtTaggedPointer();
    bool isClass();

    // object may have associated objects?
    bool hasAssociatedObjects();
    void setHasAssociatedObjects();

    // object may be weakly referenced?
    bool isWeaklyReferenced();
    void setWeaklyReferenced_nolock();

    // object may have -.cxx_destruct implementation?
    bool hasCxxDtor();

    // Optimized calls to retain/release methods
    id retain();
    void release();
    id autorelease();

    // Implementations of retain/release methods
    id rootRetain();
    bool rootRelease();
    id rootAutorelease();
    bool rootTryRetain();
    bool rootReleaseShouldDealloc();
    uintptr_t rootRetainCount();

    // Implementation of dealloc methods
    bool rootIsDeallocating();
    void clearDeallocating();
    void rootDealloc();

private:
    void initIsa(Class newCls, bool nonpointer, bool hasCxxDtor);

    // Slow paths for inline control
    id rootAutorelease2();
    bool overrelease_error();

#if SUPPORT_NONPOINTER_ISA
    // Unified retain count manipulation for nonpointer isa
    id rootRetain(bool tryRetain, bool handleOverflow);
    bool rootRelease(bool performDealloc, bool handleUnderflow);
    id rootRetain_overflow(bool tryRetain);
    bool rootRelease_underflow(bool performDealloc);

    void clearDeallocating_slow();

    // Side table retain count overflow for nonpointer isa
    void sidetable_lock();
    void sidetable_unlock();

    void sidetable_moveExtraRC_nolock(size_t extra_rc, bool isDeallocating, bool weaklyReferenced);
    bool sidetable_addExtraRC_nolock(size_t delta_rc);
    size_t sidetable_subExtraRC_nolock(size_t delta_rc);
    size_t sidetable_getExtraRC_nolock();
#endif

    // Side-table-only retain count
    bool sidetable_isDeallocating();
    void sidetable_clearDeallocating();

    bool sidetable_isWeaklyReferenced();
    void sidetable_setWeaklyReferenced_nolock();

    id sidetable_retain();
    id sidetable_retain_slow(SideTable& table);

    uintptr_t sidetable_release(bool performDealloc = true);
    uintptr_t sidetable_release_slow(SideTable& table, bool performDealloc = true);

    bool sidetable_tryRetain();

    uintptr_t sidetable_retainCount();
#if DEBUG
    bool sidetable_present();
#endif
};
```

#### weak的底层实现原理

在runtime源码中的NSObject.mm 文件中找到了关于初始化和管理weak表的方法。

```
 1 初始化时：runtime会调用objc_initWeak函数，初始化一个新的weak指针指向对象的地址。 
 
 2 添加引用时：objc_initWeak函数会调用 storeWeak() 函数， storeWeak() 的作用是更新指针指
 向，创建对应的弱引用表。 
 
 3 释放时,调用clearDeallocating函数。clearDeallocating函数首先根据对象地址获取所有weak指
 针地址的数组，然后遍历这个数组把其中的数据设为nil，最后把这个entry从weak表中删除，最后清理对象
 的记录。 
```

1. 初始化

```
/** 
 * Initialize a fresh weak pointer to some object location. 
 * It would be used for code like: 
 *
 * (The nil case) 
 * __weak id weakPtr;
 * (The non-nil case) 
 * NSObject *o = ...;
 * __weak id weakPtr = o;
 * 
 * This function IS NOT thread-safe with respect to concurrent 
 * modifications to the weak variable. (Concurrent weak clear is safe.)
 *
 * @param location Address of __weak ptr. 
 * @param newObj Object ptr. 
 */
id objc_initWeak(id *location, id newObj)
{
    if (!newObj) {
        *location = nil; // newObj 为空时直接把指针释放掉
        return nil;
    }

    return storeWeak<DontHaveOld, DoHaveNew, DoCrashIfDeallocating>
        (location, (objc_object*)newObj); // 存储weak对象
}

```


2. 更新指针指向，创建对应的弱引用表

我们storeWeak表面意思是weak商店，其实weak应该是在storeWeak函数里面进行了进一步的处理，

```
	
//我们看传参
//1、location地址
//2、newObj新的对象
template <HaveOld haveOld, HaveNew haveNew,
          CrashIfDeallocating crashIfDeallocating>
static id storeWeak(id *location, objc_object *newObj)
{

	assert(haveOld  ||  haveNew);
	if (!haveNew) assert(newObj == nil);
	// 该过程用来更新弱引用指针的指向
	// 初始化 previouslyInitializedClass 指针
	Class previouslyInitializedClass = nil;
	id oldObj;
	
	//声明新旧两个SideTable散列表
	SideTable *oldTable;
	SideTable *newTable;
	// 获得新值和旧值得锁存位置（用地址作为唯一标示）
	// 通过地址来建立索引标志，防止重复
	// 下面
	 
retry:
	if (haveOld) {
		// 更改指针，获得以 oldObj 为索引所存储的值地址
		oldObj = *location;
		oldTable = &SideTables()[oldObj];
	} else {
		oldTable = nil;
	}
	if (haveNew) {
		// 更改新值指针，获得以 newObj 为索引做hash运算得到的SideTable
		newTable = &SideTables()[newObj];
	} else {
		newTable = nil;
	}
	
	// 加锁操作，防止多线程中竞争冲突
	SideTable::lockTwo<haveOld, haveNew>(oldTable, newTable);
	// 避免线程冲突重处理
	// location 应该与 oldObj 保持一致，如果不同，说明当前的 location 已经处理过 oldObj 可是又被其他线程所修改
	if (haveOld  &&  *location != oldObj) {
		SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
		goto retry;
	}
	// 防止弱引用间死锁
	// 并且通过 +initialize 初始化构造器保证弱引用的isa非空指向
	if (haveNew  &&  newObj) {
	   // 获得新对象的isa 指针
		Class cls = newObj->getIsa();
		// 判断isa 非空且已经初始化
		if (cls != previouslyInitializedClass  &&  
		!((objc_class *)cls)->isInitialized()) 
	{
		// 解锁
			SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
		// 对其 isa 指针进行初始化
			_class_initialize(_class_getNonMetaClass(cls, (id)newObj));
		// 如果该类已经完成执行 +initialize 方法是最理想情况
		// 如果该类 +initialize 在线程中
		// 例如 +initialize 正在调用 storeWeak 方法
		// 需要手动对其增加保护策略，并设置 previouslyInitializedClass 指针进行标记
			previouslyInitializedClass = cls;
		
			goto retry;
		}
	}
	//  清除旧值
	if (haveOld) {
		weak_unregister_no_lock(&oldTable->weak_table, oldObj, location);
	}
	
	//  分配新值 
	if (haveNew) {
		newObj = (objc_object *)
		weak_register_no_lock(&newTable->weak_table, (id)newObj, location, 
		crashIfDeallocating);
	  // 如果弱引用被释放weak_register_no_lock 方法返回nil
	  // 在引用计数表中设置弱引用标记位
	if (newObj  &&  !newObj->isTaggedPointer()) {
	// 弱引用为初始化操作
	// 
		newObj->setWeaklyReferenced_nolock();
	}
		*location = (id)newObj;
	}else {
	 
	}
		SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
		return (id)newObj;
	}
```

在NSObject.mm 文件中可以查看到SideTable结构体定义

```
struct SideTable {
    spinlock_t slock; // 保证原子操作的自旋锁
    RefcountMap refcnts; // 引用计数的hash表
    weak_table_t weak_table; // weak引用全局hash表

    SideTable() {
        memset(&weak_table, 0, sizeof(weak_table));
    }

    ~SideTable() {
        _objc_fatal("Do not delete SideTable.");
    }

    void lock() { slock.lock(); }
    void unlock() { slock.unlock(); }
    void forceReset() { slock.forceReset(); }

    // Address-ordered lock discipline for a pair of side tables.

    template<HaveOld, HaveNew>
    static void lockTwo(SideTable *lock1, SideTable *lock2);
    template<HaveOld, HaveNew>
    static void unlockTwo(SideTable *lock1, SideTable *lock2);
};
```
+ 通过上面的说明我们可以看到：系统通过一个全局的weak_table_t表，采用hash（哈希表）的方式存储所有的weak对象，使用不定类型对象的地址作为key，用weak_entry_t 类型结构体对象作为value。

+ weak_entry_t （weak_table_t表中hash表的value值，weak对象体）：负责维护和存储指向一个对象的所有弱引用hash表。

+ objc_object (weak_entry_t对象中的范型对象，用于标记weak对象)：标示weak引用的对象。


#### weak_register_no_lock 底层结构

```
/** 
 * Registers a new (object, weak pointer) pair. Creates a new weak
 * object entry if it does not exist.
 * 
 * @param weak_table The global weak table.
 * @param referent The object pointed to by the weak reference.
 * @param referrer The weak pointer address.
 */
id 
weak_register_no_lock(weak_table_t *weak_table, id referent_id, 
                      id *referrer_id, bool crashIfDeallocating)
{
    objc_object *referent = (objc_object *)referent_id;
    objc_object **referrer = (objc_object **)referrer_id;

    if (!referent  ||  referent->isTaggedPointer()) return referent_id;

    // ensure that the referenced object is viable
    bool deallocating;
    if (!referent->ISA()->hasCustomRR()) {
        deallocating = referent->rootIsDeallocating();
    }
    else {
        BOOL (*allowsWeakReference)(objc_object *, SEL) = 
            (BOOL(*)(objc_object *, SEL))
            object_getMethodImplementation((id)referent, 
                                           SEL_allowsWeakReference);
        if ((IMP)allowsWeakReference == _objc_msgForward) {
            return nil;
        }
        deallocating =
            ! (*allowsWeakReference)(referent, SEL_allowsWeakReference);
    }

    if (deallocating) {
        if (crashIfDeallocating) {
            _objc_fatal("Cannot form weak reference to instance (%p) of "
                        "class %s. It is possible that this object was "
                        "over-released, or is in the process of deallocation.",
                        (void*)referent, object_getClassName((id)referent));
        } else {
            return nil;
        }
    }

    // now remember it and where it is being stored
    weak_entry_t *entry;
    if ((entry = weak_entry_for_referent(weak_table, referent))) {
        append_referrer(entry, referrer);
    } 
    else {
        weak_entry_t new_entry(referent, referrer);
        weak_grow_maybe(weak_table);
        weak_entry_insert(weak_table, &new_entry);
    }

    // Do not set *referrer. objc_storeWeak() requires that the 
    // value not change.

    return referent_id;
}
```

##### 查看其中关键代码weak_entry_for_referent:

```
/** 
 * Return the weak reference table entry for the given referent. 
 * If there is no entry for referent, return NULL. 
 * Performs a lookup.
 *
 * @param weak_table 
 * @param referent The object. Must not be nil.
 * 
 * @return The table of weak referrers to this object. 
 */
static weak_entry_t *
weak_entry_for_referent(weak_table_t *weak_table, objc_object *referent)
{
    assert(referent);

    weak_entry_t *weak_entries = weak_table->weak_entries;

    if (!weak_entries) return nil;

    size_t begin = hash_pointer(referent) & weak_table->mask;
    size_t index = begin;
    size_t hash_displacement = 0;
    while (weak_table->weak_entries[index].referent != referent) {
        index = (index+1) & weak_table->mask;
        if (index == begin) bad_weak_table(weak_table->weak_entries);
        hash_displacement++;
        if (hash_displacement > weak_table->max_hash_displacement) {
            return nil;
        }
    }
    
    return &weak_table->weak_entries[index];
}
```
> 上面代码是找到weak_table->wak_entries，然后将引用计数对象的地址进行hash化后与weak_table->mask 做“与“操作，作为下标，在全局weak表中查找，若找到，返回这个对象的weak记录表，若没有，返回nil。 
> 
> 
> 外面拿到这个weak_entry_t *entry,执行append_referer(entry,referer),把这个referer插入到entry里面。
> 
> 如果返回的这个entry是空的话，就会创建一个新的entry，然后分配空间，最后把这个entry插入weak_table里面的weak_entries数组里面。



+ 初始化弱引用对象流程一览

弱引用的初始化，从上文的分析中可以看出，主要的操作部分就在弱引用表的取键、查询散列、创建弱引用表等操作，可以总结如下的流程：

![](https://upload-images.jianshu.io/upload_images/2664540-255e050c9fafa044.png?imageMogr2/auto-orient/)

对weak进行存储的过程可以通过下边的流程图详细的描述出来：

![](https://upload-images.jianshu.io/upload_images/2664540-2d3b53046d67d907.png?imageMogr2/auto-orient/strip%7CimageView2/2/w/600)


#### weak释放为nil的过程

> weak 被释放为nil，整个对象的释放整体流程为：

+ 1、 调用objc_relase
+ 2、因为对象的引用计数为0，所以执行dealloc
+ 3、在dealloc中，调用了\_objc\_rootDealloc函数。

```
// Replaced by NSZombies
- (void)dealloc {
    _objc_rootDealloc(self);
}
```
+ 4、在\_objc\_rootDealloc中，调用了object\_dispose函数

```
// objc-runtime-new.mm 文件
/***********************************************************************
* object_dispose
* fixme
* Locking: none
**********************************************************************/
id 
object_dispose(id obj)
{
    if (!obj) return nil;

    objc_destructInstance(obj);    
    free(obj);

    return nil;
}
```

+ 5、调用objc\_destructInstance

```
// objc-runtime-new.mm 文件
/***********************************************************************
* objc_destructInstance
* Destroys an instance without freeing memory. 
* Calls C++ destructors.
* Calls ARC ivar cleanup.
* Removes associative references.
* Returns `obj`. Does nothing if `obj` is nil.
**********************************************************************/
void *objc_destructInstance(id obj) 
{
    if (obj) {
        // Read all of the flags at once for performance.
        bool cxx = obj->hasCxxDtor();
        bool assoc = obj->hasAssociatedObjects();// 

        // This order is important.
        if (cxx) object_cxxDestruct(obj);
        if (assoc) _object_remove_assocations(obj);
        obj->clearDeallocating();
    }

    return obj;
}
```

+ 6、调用clearDeallocating。

```
// NSObject.mm 文件
inline void 
objc_object::clearDeallocating()
{
    if (slowpath(!isa.nonpointer)) {
        // Slow path for raw pointer isa.
        sidetable_clearDeallocating();
    }
    else if (slowpath(isa.weakly_referenced  ||  isa.has_sidetable_rc)) {
        // Slow path for non-pointer isa with weak refs and/or side table data.
        clearDeallocating_slow();
    }

    assert(!sidetable_present());
}
```

+ 7、调用sidetable_clearDeallocating函数

```
// NSObject.mm 文件
void 
objc_object::sidetable_clearDeallocating()
{
    SideTable& table = SideTables()[this];

    // clear any weak table items
    // clear extra retain count and deallocating bit
    // (fixme warn or abort if extra retain count == 0 ?)
    table.lock();
    RefcountMap::iterator it = table.refcnts.find(this);
    if (it != table.refcnts.end()) {
        if (it->second & SIDE_TABLE_WEAKLY_REFERENCED) {
            weak_clear_no_lock(&table.weak_table, (id)this);
        }
        table.refcnts.erase(it);
    }
    table.unlock();
}
```

+ 8、调用

```
// NSObject.mm 文件
// Slow path of clearDeallocating() 
// for objects with nonpointer isa
// that were ever weakly referenced 
// or whose retain count ever overflowed to the side table.
NEVER_INLINE void
objc_object::clearDeallocating_slow()
{
    assert(isa.nonpointer  &&  (isa.weakly_referenced || isa.has_sidetable_rc));

    SideTable& table = SideTables()[this];
    table.lock();
    if (isa.weakly_referenced) {
        weak_clear_no_lock(&table.weak_table, (id)this);
    }
    if (isa.has_sidetable_rc) {
        table.refcnts.erase(this);
    }
    table.unlock();
}
```

对weak置为nil的操作最终调用执行weak\_clear\_no\_lock方法用于执行置为nil的操作，实现如下

```
// objc-weak.mm 文件
/** 
 * Called by dealloc; nils out all weak pointers that point to the 
 * provided object so that they can no longer be used.
 * 
 * @param weak_table 
 * @param referent The object being deallocated. 
 */
void 
weak_clear_no_lock(weak_table_t *weak_table, id referent_id) 
{
    objc_object *referent = (objc_object *)referent_id;

    weak_entry_t *entry = weak_entry_for_referent(weak_table, referent);
    if (entry == nil) {
        /// XXX shouldn't happen, but does with mismatched CF/objc
        //printf("XXX no entry for clear deallocating %p\n", referent);
        return;
    }

    // zero out references
    weak_referrer_t *referrers;
    size_t count;
    
    if (entry->out_of_line()) {
        referrers = entry->referrers;
        count = TABLE_SIZE(entry);
    } 
    else {
        referrers = entry->inline_referrers;
        count = WEAK_INLINE_COUNT;
    }
    
    for (size_t i = 0; i < count; ++i) {
        objc_object **referrer = referrers[i];
        if (referrer) {
            if (*referrer == referent) {
                *referrer = nil;
            }
            else if (*referrer) {
                _objc_inform("__weak variable at %p holds %p instead of %p. "
                             "This is probably incorrect use of "
                             "objc_storeWeak() and objc_loadWeak(). "
                             "Break on objc_weak_error to debug.\n", 
                             referrer, (void*)*referrer, (void*)referent);
                objc_weak_error();
            }
        }
    }
    
    weak_entry_remove(weak_table, entry);
}
```


objc\_clear\_deallocating 该函数的动作如下：

+ 1、 从weak表中获取废弃对象的地址为键值得记录。
+ 2、将包含在记录中的所有附有weak修饰符变量的地址，赋值为nil。
+ 3、将weak表中该记录删除。
+ 4、从引用计数中删除废弃对象的地址为键值得记录。




